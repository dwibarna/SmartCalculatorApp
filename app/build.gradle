plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    namespace 'com.sobarna.smartcalculatorapp'
    compileSdk 33

    defaultConfig {
        applicationId "com.sobarna.smartcalculatorapp"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {

        all {
            buildConfigField "String", "MASTER_KEY_PRE_M", MASTER_KEY_PRE_M
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "color", "type"

    productFlavors {
        red {
            dimension "color"
            applicationIdSuffix ".red"
        }
        green {
            dimension "color"
            applicationIdSuffix ".green"
        }
        filesystem {
            dimension "type"
            buildConfigField "boolean", "TYPE_INPUT_DATA", "true"
            applicationIdSuffix ".filesystem"
        }
        'built-in-camera' {
            dimension "type"
            buildConfigField "boolean", "TYPE_INPUT_DATA", "false"
            applicationIdSuffix ".builtincamera"
        }
    }

    applicationVariants.all { variant ->
        def flavors = ""
        variant.productFlavors.each { flavor ->
            flavors += "${flavor.name}-"
        }
        flavors = flavors.substring(0, flavors.length() - 1)
        variant.outputs.all {
            def fileName = "app-$flavors"
            outputFileName = "${fileName}.apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    viewBinding {
        enabled = true
    }

}


dependencies {

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

//    implementation 'com.google.android.gms:play-services-mlkit-text-recognition:16.0.0'
    implementation 'com.google.mlkit:text-recognition:16.0.0'
    implementation 'org.mariuszgromada.math:MathParser.org-mXparser:4.4.2'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"

    implementation "androidx.recyclerview:recyclerview:1.3.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.fragment:fragment-ktx:1.5.7"

    implementation "androidx.room:room-ktx:2.5.1"
    implementation "androidx.room:room-runtime:2.6.0-alpha01"
    kapt "androidx.room:room-compiler:2.6.0-alpha01"

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.6.1"
    implementation 'com.github.bumptech.glide:glide:4.15.1'

    implementation "com.google.dagger:hilt-android:2.46"
    kapt "com.google.dagger:hilt-android-compiler:2.46"

    implementation "androidx.security:security-crypto:1.1.0-alpha06"

    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'

}